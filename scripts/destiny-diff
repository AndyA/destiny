#!/usr/bin/env perl

use autodie;
use strict;
use warnings;

use JSON;
use Path::Class;

my ( $alien, @mf ) = map { by_hash( load_json($_) ) } @ARGV;

my %need = ();
for my $mf (@mf) {
  my $root = dir $mf->{meta}{root};
  for my $hash ( grep { !$alien->{object}{$_} } keys %{ $mf->{object} } ) {
    push @{ $need{$hash} },
     map { file( $root, $_->{name} )->relative->stringify }
     @{ $mf->{object}{$hash} };
  }
}

print "$_\n" for sort map { $_->[0] } values %need;

sub by_hash {
  my $mf      = shift;
  my $by_hash = {};
  $by_hash->{meta} = $mf->{meta};
  for my $obj ( @{ $mf->{object} } ) {
    push @{ $by_hash->{object}{ $obj->{hash} } }, $obj;
  }
  return $by_hash;
}

sub load_json { JSON->new->utf8->decode( scalar file(shift)->slurp ) }

# vim:ts=2:sw=2:sts=2:et:ft=perl

